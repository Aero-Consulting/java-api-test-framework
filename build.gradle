plugins {
    id 'java'
    id "io.qameta.allure" version "2.10.0"
}

group 'com.aero.test'
version '1.0'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

// dependency versions
def lombokVersion = "1.18.24"
def allureVersion = "2.19.0"
def testngVersion = "7.6.1"
def restAssuredVersion = "5.1.1"

dependencies {
    testImplementation "org.projectlombok:lombok:$lombokVersion"
    testImplementation "io.qameta.allure:allure-java-commons:$allureVersion"
    testImplementation "io.qameta.allure:allure-rest-assured:$allureVersion"
    testImplementation "io.qameta.allure:allure-testng:$allureVersion"
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "io.rest-assured:json-schema-validator:$restAssuredVersion"
    testImplementation "org.testng:testng:$testngVersion"
    testImplementation 'org.aspectj:aspectjweaver:1.9.9.1'
    testImplementation "org.slf4j:slf4j-simple:1.7.36"
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

allure {
    version = allureVersion
    adapter {
        frameworks {
            testng
        }
    }
}

test {
    systemProperties = System.properties
    def suiteProperty = System.getProperty("suite", "all")
    def suite = "src/test/resources/suites/${suiteProperty}.xml"
    ignoreFailures = false
    useTestNG {
        suites suite
    }
}

tasks.named('wrapper') {
    distributionUrl="https://services.gradle.org/distributions/gradle-7.5-bin.zip"
}